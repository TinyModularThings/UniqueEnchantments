buildscript {
    repositories {
        jcenter()
        mavenCentral();
        maven { url = "https://files.minecraftforge.net/maven" }

    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0"
group = "uniquee"
archivesBaseName = "Unique Enchantments"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "snapshot_20171003"
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
}

dependencies {
	compile "curse.maven:firstaid-276837:2790027"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid':"uniquee", 'name':"Unique Enchantments", 'description':"A Set of Unique Enchantments"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    version = uniquee_version
    exclude "uniqueeutils", "assets/uniqueeutil", "uniqueebattle", "assets/uniqueebattle"
}

task utilsJar(type: Jar) {
    baseName = 'Unique Enchantments Utils'
    version = unique_utils_version
    from(sourceSets.main.output) {
        exclude 'uniquee', 'assets/uniquee', 'mcmod.info', 'uniqueebattle', 'assets/uniqueebattle'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid':"uniqueeutils", 'name':"Unique Enchantments Utilities", 'description':"A Set of Unique Utility Enchantments"
    }
}

task battleJar(type: Jar) {
    baseName = 'Unique Enchantments Battle'
    version = unique_battle_version
    from(sourceSets.main.output) {
        exclude 'uniquee', 'assets/uniquee', 'mcmod.info', 'uniqueeutils', 'assets/uniqueeutil'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'modid':"uniqueebattle", 'name':"Unique Enchantments Battle", 'description':"A Set of Unique Battle Enchantments"
    }
}


reobf {
    utilsJar {
    	spec -> spec.classpath = sourceSets.main.compileClasspath
    }
    battleJar {
    	spec -> spec.classpath = sourceSets.main.compileClasspath
    }
}

artifacts {
    archives utilsJar
    archives battleJar
}